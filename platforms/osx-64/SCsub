from __future__ import print_function
import os, glob, shutil
from SCons.Errors import UserError


Import("env")


env["bits"] = "64"
env.Append(CFLAGS="-m64")
env.Append(LINKFLAGS="-m64")


### Godot binary (to run tests) ###


if not env["godot_binary"]:
    env["godot_binary"] = File("godot.osx.opt.debug.fat")
    env.Command(
        env["godot_binary"],
        None,
        "curl -L %s/godot.osx.opt.debug.fat -o ${TARGET} && chmod 750 ${TARGET}"
        % env["godot_release_base_url"],
    )
    env.NoClean(env["godot_binary"])


### GDnative stuff ###


if not env["gdnative_include_dir"]:
    env["gdnative_include_dir"] = Dir("../gdnative/include")
if not env["gdnative_wrapper_lib"]:
    env["gdnative_wrapper_lib"] = File(
        "../gdnative/libgdnative_wrapper_code.osx.opt.debug.fat.a"
    )
    env.Command(
        env["gdnative_wrapper_lib"],
        None,
        "curl -L %s/libgdnative_wrapper_code.osx.opt.fat.a -o ${TARGET}"
        % env["godot_release_base_url"],
    )
    env.NoClean(env["gdnative_wrapper_lib"])


### Python interpreter ###


if env["backend"] == "cpython":
    cpython_src = Dir("cpython")
    env.Command(
        cpython_src,
        None,
        "git clone https://github.com/python/cpython.git --depth=1 --branch=v${CPYTHON_VERSION} --single-branch ${TARGET}",
    )
    env.NoClean(cpython_src)

    cpython_version = env["CPYTHON_VERSION"]
    cpython_major_minor = re.match(r"^[0-9]\.[0-9]+", cpython_version).group(0)

    cpython_build = Dir("cpython_build")
    # TODO: allow to compile cpython with `--with-pydebug` ?
    # Compile CPython and install cffi through pip
    env.Command(
        cpython_build,
        cpython_src,
        [
            "cd ${SOURCE} && " + "echo Configuring CPython... && "
            '1>/dev/null ./configure --enable-shared --prefix=${TARGET.get_abspath()} --with-openssl=/usr/local/opt/openssl CPPFLAGS="-I/usr/local/opt/zlib/include" LDFLAGS="-L/usr/local/opt/zlib/lib" && '
            + "echo Building CPython... && "
            "1>/dev/null make -j4 && "
            + "echo Installing CPython in ${TARGET.get_abspath()}... && "
            "1>/dev/null make install && "
            + "LD_LIBRARY_PATH=${TARGET.get_abspath()}/lib ${TARGET.get_abspath()}/bin/pip3 install cffi",
            Chmod(
                "%s/lib/libpython%sm.dylib"
                % (cpython_build.abspath, cpython_major_minor),
                0o600,
            ),
            "install_name_tool -id @loader_path/lib/libpython"
            + cpython_major_minor
            + "m.dylib "
            + "%s/lib/libpython%sm.dylib"
            % (cpython_build.abspath, cpython_major_minor),
        ],
    )
    env.NoClean(cpython_build)

    def generate_build_dir(target, source, env):
        target = target[0]
        cpython_build = source[0]
        libpythonscript = source[1]
        godot_embedded = source[2]

        if os.path.isdir(target.path):
            shutil.rmtree(target.path)
        os.mkdir(target.path)

        def c(subpath=""):
            return os.path.join(cpython_build.abspath, *subpath.split("/"))

        def p(subpath=""):
            return os.path.join(target.abspath, "pythonscript", *subpath.split("/"))

        os.mkdir(p())

        shutil.copy(libpythonscript.path, p())
        open(p(".gdignore"), "w").close()

        if os.path.isdir(c("include")):
            # Windows build of CPython doesn't contain include dir
            shutil.copytree(c("include"), p("include"))

        # Remove __pycache__ to save lots of space
        for root, dirs, files in os.walk(c("lib")):
            if "__pycache__" in dirs:
                shutil.rmtree(os.path.join(root, "__pycache__"))

        shutil.copytree(c("bin"), p("bin"))

        shutil.copytree(c("lib"), p("lib"))
        if env["compressed_stdlib"]:
            shutil.move(
                p("lib/python" + cpython_major_minor),
                p("lib/tmp_python" + cpython_major_minor),
            )
            os.mkdir(p("lib/python" + cpython_major_minor))
            shutil.move(
                p("lib/tmp_python" + cpython_major_minor + "/lib-dynload"),
                p("lib/python" + cpython_major_minor + "/lib-dynload"),
            )
            shutil.move(
                p("lib/tmp_python" + cpython_major_minor + "/site-packages"),
                p("lib/python" + cpython_major_minor + "/site-packages"),
            )
            shutil.make_archive(
                base_name=p("lib/python" + cpython_major_minor),
                format="zip",
                root_dir=p("lib/tmp_python" + cpython_major_minor),
            )
            shutil.rmtree(p("lib/tmp_python" + cpython_major_minor))

        if env["dev_dyn"]:
            os.symlink(
                godot_embedded.abspath,
                p("lib/python" + cpython_major_minor + "/site-packages/godot"),
            )
        else:
            shutil.copytree(
                godot_embedded.path,
                p("lib/python" + cpython_major_minor + "/site-packages/godot"),
            )

        if "generate_build_dir_hook" in env:
            env["generate_build_dir_hook"](target.abspath)

    env["generate_build_dir"] = generate_build_dir
    env["backend_dir"] = cpython_build
    env.Append(CFLAGS="-DBACKEND_CPYTHON")
    env.Append(
        CFLAGS="-I %s/include/python%sm/" % (cpython_build.path, cpython_major_minor)
    )
    env.Append(LIBPATH="%s/lib" % cpython_build.path)
    env.Append(LIBS=["python%sm" % cpython_major_minor])
    env.Append(LINKFLAGS=["-Wl,-rpath,'$$ORIGIN/lib'"])

else:  # pypy

    PYPY_SRC_NAME = "pypy3-v6.0.0-osx64"
    PYPY_SRC_ARCHIVE = "%s.tar.bz2" % PYPY_SRC_NAME
    PYPY_SRC_ARCHIVE_URL = (
        "https://bitbucket.org/pypy/pypy/downloads/%s" % PYPY_SRC_ARCHIVE
    )

    pypy_build = Dir(PYPY_SRC_NAME)

    env.Command(
        PYPY_SRC_ARCHIVE, None, "curl -L %s -o ${TARGET}" % PYPY_SRC_ARCHIVE_URL
    )
    env.NoClean(PYPY_SRC_ARCHIVE)
    env.Command(
        pypy_build,
        PYPY_SRC_ARCHIVE,
        [
            "tar xf ${SOURCE} -C ${TARGET.srcdir}",
            "install_name_tool -id @loader_path/bin/libpypy3-c.dylib "
            + "%s/bin/libpypy3-c.dylib" % pypy_build.abspath,
        ],
    )

    def generate_build_dir(target, source, env):
        target = target[0]
        pypy_build = source[0]
        libpythonscript = source[1]
        godot_embedded = source[2]

        if os.path.isdir(target.path):
            shutil.rmtree(target.path)
        os.mkdir(target.path)

        def c(subpath=""):
            return os.path.join(pypy_build.abspath, *subpath.split("/"))

        def p(subpath=""):
            return os.path.join(target.abspath, "pythonscript", *subpath.split("/"))

        shutil.copytree(c(), p())
        os.unlink(p("LICENSE"))
        os.unlink(p("README.rst"))
        shutil.copy(libpythonscript.path, p())
        open(p(".gdignore"), "w").close()

        if env["dev_dyn"]:
            os.symlink(godot_embedded.abspath, p("site-packages/godot"))
        else:
            shutil.copytree(godot_embedded.path, p("site-packages/godot"))

        if "generate_build_dir_hook" in env:
            env["generate_build_dir_hook"](target.abspath)

    env["generate_build_dir"] = generate_build_dir
    env["backend_dir"] = pypy_build
    env.Append(CFLAGS="-DBACKEND_PYPY")
    env.Append(CFLAGS="-I %s/include" % pypy_build.path)
    env.Append(LIBPATH="%s/bin" % pypy_build.path)
    env.Append(LIBS=["pypy3-c"])
    env.Append(LINKFLAGS=["-Wl,-rpath,'$$ORIGIN/bin'"])
